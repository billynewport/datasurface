plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'maven-publish'
}

group 'org.datasurface'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

def grpcVersion = '1.69.0'
def protobufVersion = '4.29.3'
def sparkVersion = '3.5.1'
def sparkCoreVersion = '2.12'
def icebergVersion = '1.5.1'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir '../../src/datasurface/api'
        }
        java {
            srcDirs 'src/main/java', 'src/generated/main/java', 'src/generated/main/grpc'
        }
    }
}

dependencies {
    // https://mvnrepository.com/artifact/com.holdenkarau/spark-testing-base
    implementation group: 'com.holdenkarau', name: "spark-testing-base_${sparkCoreVersion}", version: "${sparkVersion}_${icebergVersion}"

//    implementation "org.apache.spark:spark-core_${sparkCoreVersion}:${sparkVersion}"
//    implementation "org.apache.spark:spark-sql_${sparkCoreVersion}:${sparkVersion}"
    implementation group: 'org.apache.iceberg', name: "iceberg-spark-runtime-3.5_${sparkCoreVersion}", version: "${icebergVersion}"
    implementation group: 'org.apache.iceberg', name: 'iceberg-hive-metastore', version: "${icebergVersion}"
    implementation "org.apache.iceberg:iceberg-core:${icebergVersion}"
// https://mvnrepository.com/artifact/org.apache.hive/hive-metastore
//    implementation ('org.apache.hive:hive-metastore:3.1.3')
//    {
//        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
//        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
//        exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
//    }
// https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-slf4j2-impl

//    implementation 'com.amazonaws:aws-java-sdk:1.11.1000'
//    implementation 'org.apache.hadoop:hadoop-aws:3.2.0'
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation 'commons-cli:commons-cli:1.6.0'

    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.12.0'

    // Generated protobuf stubs require this in later JDKs
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'org.datasurface.dataplatform.awsbatch.dmsingestor'
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/billynewport/datasurface") // Replace OWNER with your GitHub username and REPOSITORY with your GitHub repository name
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

compileJava {
    dependsOn generateProto
}

clean {
    delete "src/generated"
}
